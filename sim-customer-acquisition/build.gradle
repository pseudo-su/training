buildscript {
    ext.kotlin_version = "1.3.50"
    ext.ktor_version = "1.2.4"
    ext.logback_version = "1.2.3"
    ext.slf4j_version = "1.7.25"
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
    }
}

apply plugin: "kotlin"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "application"
apply plugin: "idea"

idea.module {
    scopes.TEST.plus += [ configurations.testCompile ]
}


mainClassName = "com.sim.customer.acquisition.SimpleProducerKt"

sourceSets {
    main {
        kotlin {
            srcDirs = ["src"]
            exclude "**/*.test.kt"
        }
        resources {
            srcDirs = [ "resources" ]
        }
    }
    test {
        kotlin {
            srcDirs = ["src"]
            include "**/*.test.kt"
        }
    }
}

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://packages.confluent.io/maven/" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // fake data generator
    compile "com.github.javafaker:javafaker:0.15"

    // logging
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "org.slf4j:slf4j-log4j12:1.7.25"

    // JSON serialization
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.6"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.6"

    // Kafka
    compile "org.apache.kafka:kafka-clients:2.0.0"
    compile "io.confluent:kafka-avro-serializer:5.0.0"
    
    // TODO: make this testCompile instead of compile without breaking IDE
    compile "junit:junit:4.11"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

// kotlin.experimental.coroutines = "enable"

shadowJar {
    archiveBaseName = "sim-customer-acquisition"
    archiveClassifier = null
    archiveVersion = null
}

task resolveDependencies {
    doLast {
        project.rootProject.allprojects.each { subProject ->
            subProject.buildscript.configurations.each { configuration ->
                if (configuration.canBeResolved) configuration.resolve()
            }
            subProject.configurations.each { configuration ->
                if (configuration.canBeResolved) configuration.resolve()
            }
        }
    }
}
